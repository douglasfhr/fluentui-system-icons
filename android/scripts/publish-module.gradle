apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
//    if (project.plugins.findPlugin("com.android.library")) {
//        from android.sourceSets.main.java.srcDirs
//    } else {
//        from sourceSets.main.java.srcDirs
//    }
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = project.POM_GROUP_ID
version = getReleaseVersion()

afterEvaluate {
    publishing {
        /*----- Adding local maven repo to generate artifact -----*/
        repositories {
            maven {
                name = "local"
                url "$buildDir/artifacts"
            }
        }
        publications {
            release(MavenPublication) {
                groupId project.POM_GROUP_ID
                artifactId project.POM_ARTIFACT_ID
                version getReleaseVersion()

//                if (project.plugins.findPlugin("com.android.library")) {
//                    artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
//                } else {
//                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
//                }
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

                artifact androidSourcesJar

                pom {
                    name = project.POM_ARTIFACT_ID
                    description = 'Fluent System Icons are a collection of familiar, friendly and modern icons from Microsoft.'
                    url = 'https://github.com/microsoft/fluentui-system-icons'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/microsoft/fluentui-system-icons/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'fluent-ui-android'
                            name = 'Microsoft Inc.'
                            email = 'fluentuinativeowners@microsoft.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/microsoft/fluentui-system-icons.git'
                        developerConnection = 'scm:git:ssh://github.com/microsoft/fluentui-system-icons.git'
                        url = 'https://github.com/microsoft/fluentui-system-icons/tree/main'
                    }
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        project.configurations.implementation.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = project.hasProperty('GPGSigningKeyID') ? "$GPGSigningKeyID" : ''
ext["signing.password"] = project.hasProperty('GPGSigningPassword') ? "$GPGSigningPassword" : ''
ext["signing.secretKeyRingFile"] = project.hasProperty('SigningSecretKeyRingFile') ? "$SigningSecretKeyRingFile" : ''

signing {
    sign publishing.publications
    required { gradle.taskGraph.hasTask("publish") }
}

// Task to generate all required checksum files for Maven Central
tasks.register('generateChecksums') {
    doLast {
        // Define which extensions to generate checksums for
        def primaryArtifacts = ['.pom', '.aar', '-sources.jar']
        def checksumTypes = ['md5', 'sha1', 'sha256', 'sha512']
        def version = getReleaseVersion()
        def artifactId = project.POM_ARTIFACT_ID
        def artifactsDir = file("$buildDir/artifacts/com/microsoft/design/$artifactId/$version")
        
        println "Generating checksums in directory: ${artifactsDir}"
        
        if (artifactsDir.exists()) {
            // First generate checksums for primary artifacts
            artifactsDir.eachFile { file ->
                def fileName = file.name
                
                // Check if this is a primary artifact
                boolean isPrimaryArtifact = primaryArtifacts.any { ext -> fileName.endsWith(ext) }
                boolean isSignature = fileName.endsWith('.asc')
                
                if (isPrimaryArtifact || isSignature) {
                    println "Processing file: ${fileName}"
                    
                    // Generate checksums for this file
                    checksumTypes.each { checksumType ->
                        def checksumFile = new File(file.absolutePath + ".$checksumType")
                        ant.checksum(file: file.absolutePath, algorithm: checksumType, 
                            fileext: ".$checksumType", forceOverwrite: true)
                        println "  Generated $checksumType checksum for $fileName"
                    }
                }
            }
            
            // Now generate checksums for checksum files (recursive)
            checksumTypes.each { checksumType1 ->
                artifactsDir.eachFileMatch(~/.*\.$checksumType1$/) { checksumFile ->
                    checksumTypes.each { checksumType2 ->
                        if (checksumType1 != checksumType2) {
                            def nestedChecksumFile = new File(checksumFile.absolutePath + ".$checksumType2")
                            if (!nestedChecksumFile.exists()) {
                                ant.checksum(file: checksumFile.absolutePath, algorithm: checksumType2, 
                                    fileext: ".$checksumType2", forceOverwrite: true)
                                println "  Generated nested $checksumType2 checksum for ${checksumFile.name}"
                            }
                        }
                    }
                }
            }
        } else {
            println "WARNING: Artifacts directory does not exist: ${artifactsDir}"
        }
    }
}

// Make the publish task depend on generating checksums
tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn generateChecksums
}

String getReleaseVersion() {
    String version = System.getenv('VERSION_NAME')
    if (version == null || version == "") {
        return project.VERSION_NAME + "-SNAPSHOT"
    }
    return version
}
